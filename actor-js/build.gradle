repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

apply plugin: 'war'
apply plugin: 'gwt'

task cleanGenSources(type: Delete) {
    delete "${projectDir}/src/gen/java/"
}

task cleanGenResources(type: Delete) {
    delete "${projectDir}/src/gen/resources/"
}

task copyGenSources(type: Copy, dependsOn: ['cleanGenSources']) {
    from fileTree("${projectDir}/../actor-common/src/main/java/")
    from fileTree("${projectDir}/../actor-bc/src/main/java/")

    into "${projectDir}/src/gen/java/"
}

task copyGenResources(type: Copy, dependsOn: ['cleanGenSources']) {
    from fileTree("${projectDir}/../actor-common/src/main/resources/")

    into "${projectDir}/src/gen/resources/"
}

tasks.compileJava.dependsOn(copyGenResources)
tasks.compileJava.dependsOn(copyGenSources)
tasks.clean.dependsOn(cleanGenSources)
tasks.clean.dependsOn(cleanGenResources)

dependencies {
    compile 'com.google.gwt:gwt-user:2.7.0'
    compile 'org.timepedia.exporter:gwtexporter:2.4.0'
    gwt 'org.timepedia.exporter:gwtexporter:2.4.0'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/gen/java']
        }
        resources {
            srcDirs = ['src/main/resources', 'src/gen/resources']
        }
    }
}

gwt {
    gwtVersion = '2.7.0'
    modules 'im.actor.ActorMessenger'

    compiler {
        strict = true;
        enableClosureCompiler = true;
//        disableClassMetadata = true;
//        disableCastChecking = true;
    }
}